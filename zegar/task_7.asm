.386P
INCLUDE STRUKT.TXT
DANE    SEGMENT USE16
	GDT_NULL 	DESKR <0,0,0,0,0,0>
	GDT_DANE 	DESKR <DANE_SIZE-1,0,0,92H,0,0>		;8
	GDT_PROGRAM DESKR <PROGRAM_SIZE-1,0,0,98H,0,0>		;16
	GDT_STOS 	DESKR <767,0,0,92H,0,0>			;24
	GDT_EKRAN 	DESKR <4095,8000H,0BH,92H,0,0>		;32
	GDT_TSS_0	DESKR <103,0,0,89H,0,0>			;40
	GDT_TSS_1	DESKR <103,0,0,89H,0,0>			;48
	GDT_TSS_2	DESKR <103,0,0,89H,0,0>			;56
	GDT_SIZE = $ - GDT_NULL 
;Tablica deskryptorÛw przerwaÒ IDT
	IDT	LABEL WORD
	INCLUDE PM_IDT.TXT
	IDT_0	INTR <PROC_0>
	IDT_1	INTR <PROC_1>
	IDT_SIZE = $ - IDT
	PDESKR	DQ 	0
	ORG_IDT	DQ	0
	TEKST	DB 'TRYB CHRONIONY'
	TEKST1	DB 'PRZERWANIE'
   	INCLUDE PM_DATA.TXT
	INFO	DB 'POWROT Z TRYBU CHRONIONEGO $'
	T0_ADDR	DW 0,40
	T1_ADDR	DW 0,48
	T2_ADDR	DW 0,56
	TSS_0	DB 104 DUP (0)
	TSS_1	DB 104 DUP (0)
	TSS_2	DB 104 DUP (0)
	INF_0	DB 'PROGRAM GLOWNY'
	INF_1	DB 'ZADANIE NR 1'
	INF_2	DB 'ZADANIE NR 2'
DANE_SIZE=$-GDT_NULL
DANE	ENDS

PROGRAM	SEGMENT 'CODE' USE16
        ASSUME CS:PROGRAM, DS:DANE, SS:STK
POCZ	LABEL WORD
INCLUDE PM_EXC.TXT
INCLUDE MAKRA.TXT
PROC_0	PROC
PROC_0	ENDP
;Procedura obs≥ugi przerwania od klawiatury (przerwanie nr 1)
PROC_1	PROC
	PUSH AX
	PUSH DX
	IN AL,60H	;Pobranie kodu klawisza
	MOV DL,AL
	IN AL,61H	;Potwierdzenie pobrania numeru klawisza
	OR AL,80H
	OUT 61H,AL
	AND AL,7FH
	OUT 61H,AL
	MOV AL,20H	;Sygna≥ koÒca obs≥ugi przerwania
	OUT 20H,AL
	CMP DL,0BH	;Klawisz '0'
	JE TSK0
	CMP DL,2	;Klawisz '1'
	JE TSK1
	CMP DL,3	;Klawisz '2'
	JE TSK2
	JMP OUT_P1
   TSK0:	JMP DWORD PTR T0_ADDR	;Prze≥πczenie zadania na zadanie 
					;nr 0 (program g≥Ûwny)
	JMP out_p1
   TSK1:	JMP DWORD PTR T1_ADDR	;Prze≥πczenie zadania na zadanie nr 1
	JMP OUT_P1
   TSK2:	JMP DWORD PTR T2_ADDR	;Prze≥πczenie zadania na zadanie nr 2
   OUT_P1:	POP DX
	POP AX
	IRETD
PROC_1	ENDP

START:	
	INICJOWANIE_DESKRYPTOROW
   PM_TSS0_I_TSS1 TSS_0,TSS_1,GDT_TSS_0,GDT_TSS_1
	XOR EAX,EAX
	MOV AX,OFFSET TSS_2
	ADD EAX,EBP
	MOV BX,OFFSET GDT_TSS_2
	MOV [BX].BASE_1,AX
	ROL EAX,16
	MOV [BX].BASE_M,AL
	MOV WORD PTR TSS_1+4CH,16		
	MOV WORD PTR TSS_1+20H,OFFSET ZADANIE1
	MOV WORD PTR TSS_1+50H,24		
	MOV WORD PTR TSS_1+38H,256
	MOV WORD PTR TSS_1+54H,8
	MOV WORD PTR TSS_1+48H,32
	STI
	PUSHFD
	POP EAX
	MOV DWORD PTR TSS_1+24H,EAX
	MOV WORD PTR TSS_2+4CH,16		
	MOV WORD PTR TSS_2+20H,OFFSET ZADANIE2
	MOV WORD PTR TSS_2+50H,24		
	MOV WORD PTR TSS_2+38H,512
	MOV WORD PTR TSS_2+54H,8
	MOV WORD PTR TSS_2+48H,32
	MOV DWORD PTR TSS_2+24H,EAX
	CLI
	INICJACJA_IDTR
	KONTROLER_PRZERWAN_PM 0FDH
	AKTYWACJA_PM
	MOV AX,32
	MOV ES,AX
	MOV GS,AX
	MOV FS,AX
	MOV AX,40			;Za≥adowanie rejestru zadania (TR)
	LTR AX				;deskryptorem segmentu stanu 
   WYPISZ_N_ZNAKOW_Z_ATRYBUTEM TEKST,14,680,ATRYB
	STI		
   C1:	MOV AH,62H
	MOV CX,2
   C2:	PUSH CX
	MOV DI,1440
	MOV CX,14
	MOV SI,OFFSET INF_0
   C3:	MOV AL,[SI]
	MOV ES:[DI],AX
	INC SI
	ADD DI,2
	PUSH CX
	MOV ECX,0FFFFFFh	;OpÛünienie
   C4:	MOV DX,0FFFEH
	ADD DX,1
	DB 67H
	LOOP C4
	POP CX	
	LOOP C3
	POP CX
	MOV AH,26H
	LOOP C2
	JMP C1
	CLI
	ETYKIETA_POWROTU_DO_RM:
	KONTROLER_PRZERWAN_RM
	MIEKI_POWROT_RM
	POWROT_DO_RM 0,1

ZADANIE1 PROC
   A1:	MOV AH,71H
	MOV CX,2
   A2:	PUSH CX
	MOV DI,800
	MOV CX,12
	MOV SI,OFFSET INF_1
   A3:	MOV AL,[SI]
	MOV ES:[DI],AX
	INC SI
	ADD DI,2
	PUSH CX
	MOV ECX,0FFFFFFH	;OP”èNIENIE
   A4:	MOV DX,0FFFEH
	ADD DX,1
	DB 67H
	LOOP A4
	POP CX	
	LOOP A3
	POP CX
	MOV AH,17H
	LOOP A2
	JMP A1
ZADANIE1 ENDP

ZADANIE2 PROC
   B1:	MOV AH,34H
	MOV CX,2
   B2:	PUSH CX
	MOV DI,1120
	MOV CX,12
	MOV SI,OFFSET INF_2
	CLD
   B3:	LODSB
	STOSW
	PUSH CX
	MOV ECX,0FFFFFFH	;OP”èNIENIE
   B4:	MOV DX,0FFFFH
	ADD DX,1
	DB 67H
	LOOP B4
	POP CX	
	LOOP B3
	POP CX
	MOV AH,43H
	LOOP B2
	JMP B1
ZADANIE2 ENDP
PROGRAM_SIZE=$-POCZ
PROGRAM ENDS
STK	SEGMENT STACK 'STACK'
	DB 256*3 DUP(0)
STK	ENDS
END START

